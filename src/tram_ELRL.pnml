/*
#    OpenTTD Galician Roads, Trams & Vehicles (GALRTV) -  Copyright (C) 2021  Pablo Villaverde Castro
#    This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
#    This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
#	 You should have received a copy of the GNU General Public License along with this program.  If not, see <https://github.com/pvillaverde/openttd-spanish-railsets/blob/main/LICENSE>.
*/
spriteset (ELRL_guiSpriteset, "src/gfx/roads/ELRL/gui.gimp.png") { roadGuiTemplate() }

spriteset (ELRL_underlaySpriteset, "src/gfx/roads/RAIL/underlay.gimp.png") { roadUnderlayTemplate() }
spriteset (ELRL_underlaySnowSpriteset, "src/gfx/roads/RAIL/underlaySnow.gimp.png") { roadUnderlayTemplate() }
spriteset (ELRL_overlaySpriteset, "src/gfx/roads/RAIL/overlay.gimp.png") { roadUnderlayTemplate() }

spriteset (ELRL_bridgesSpriteset, "src/gfx/roads/RAIL/bridges.gimp.png") { roadBridgeOverlayTemplate() }
spriteset (ELRL_bridgesSnowSpriteset, "src/gfx/roads/RAIL/bridgesSnow.gimp.png") { roadBridgeOverlayTemplate() }

spriteset (ELRL_depotsSpriteset, "src/gfx/roads/ELRL/depots.gimp.png") { roadDepotsTemplate() }
spriteset (ELRL_depotsSnowSpriteset, "src/gfx/roads/ELRL/depotsSnow.gimp.png") { roadDepotsTemplate() }


spriteset (ELRL_catenaryFronOldSpriteset, "src/gfx/roads/ELRL/catenaryFronOld.gimp.png") { roadCatenaryTemplate() }
spriteset (ELRL_catenaryFronModSpriteset, "src/gfx/roads/ELRL/catenaryFronMod.gimp.png") { roadCatenaryTemplate() }
spriteset (ELRL_catenaryFronCorSpriteset, "src/gfx/roads/ELRL/catenaryFronCor.gimp.png") { roadCatenaryTemplate() }
spriteset (ELRL_catenaryBackOldSpriteset, "src/gfx/roads/ELRL/catenaryBackOld.gimp.png") { roadCatenaryTemplate() }
spriteset (ELRL_catenaryBackModSpriteset, "src/gfx/roads/ELRL/catenaryBackMod.gimp.png") { roadCatenaryTemplate() }
spriteset (ELRL_catenaryBackCorSpriteset, "src/gfx/roads/ELRL/catenaryBackCor.gimp.png") { roadCatenaryTemplate() }

switch (FEAT_TRAMTYPES, SELF, ELRL_bridgesTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: ELRL_bridgesSnowSpriteset;
	ELRL_bridgesSpriteset;
}
switch (FEAT_TRAMTYPES, SELF, ELRL_depotsTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: ELRL_depotsSnowSpriteset;
	ELRL_depotsSpriteset;
}
switch (FEAT_TRAMTYPES, SELF, ELRL_underlayTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: ELRL_underlaySnowSpriteset;
	ELRL_underlaySpriteset;
}

switch(FEAT_TRAMTYPES,	SELF,	ELRL_catenaryFrontYearSwitch, current_year){ 
	0 .. 1970 - 1	: ELRL_catenaryFronOldSpriteset ;
	ELRL_catenaryFronModSpriteset;
}

switch (FEAT_TRAMTYPES, SELF, ELRL_catenaryFrontTownZoneSwitch, town_zone) {
	TOWNZONE_CENTRE: ELRL_catenaryFronCorSpriteset;
	TOWNZONE_INNER_SUBURB: ELRL_catenaryFronCorSpriteset;
	TOWNZONE_OUTER_SUBURB: ELRL_catenaryFronCorSpriteset;
	TOWNZONE_OUTSKIRT: ELRL_catenaryFronCorSpriteset;
	ELRL_catenaryFrontYearSwitch;
}

switch(FEAT_TRAMTYPES,	SELF,	ELRL_catenaryBackYearSwitch, current_year){ 
	0 .. 1970 - 1	: ELRL_catenaryBackOldSpriteset ;
	ELRL_catenaryBackModSpriteset;
}

switch (FEAT_TRAMTYPES, SELF, ELRL_catenaryBackTownZoneSwitch, town_zone) {
	TOWNZONE_CENTRE: ELRL_catenaryBackCorSpriteset;
	TOWNZONE_INNER_SUBURB: ELRL_catenaryBackCorSpriteset;
	TOWNZONE_OUTER_SUBURB: ELRL_catenaryBackCorSpriteset;
	TOWNZONE_OUTSKIRT: ELRL_catenaryBackCorSpriteset;
	ELRL_catenaryBackYearSwitch;
}


if (param_speed_units == SPEED_UNITY_MPH) {
	item(FEAT_TRAMTYPES, item_tram_ELRL) {
		property {	
			speed_limit:                 param_speedlimit_ELRL mph;
		}
	}
}
if (param_speed_units == SPEED_UNITY_KMH) {
	item(FEAT_TRAMTYPES, item_tram_ELRL) {
		property {	
			speed_limit:                 param_speedlimit_ELRL km/h;
		}
	}
}
if (param_speed_units == SPEED_UNITY_MPS) {
	item(FEAT_TRAMTYPES, item_tram_ELRL) {
		property {	
			speed_limit:                 param_speedlimit_ELRL m/s;
		}
	}
}

item(FEAT_TRAMTYPES, item_tram_ELRL) {
	property {
		label:                       "ELRL";
		introduction_date:           (param_intro_year == 0) ? date(1900,01,01) : 0;
		name:                        string(STR_TRAM_ELRL_NAME);
		toolbar_caption:             string(STR_TRAM_ELRL_TOOLBAR);
		menu_text:                   string(STR_TRAM_ELRL_MENU);
		build_window_caption:        string(STR_TRAM_BUILD_VEHICLE);
		autoreplace_text:            string(STR_TRAM_AUTOREPLACE);
		new_engine_text:             string(STR_TRAM_NEW_ENGINE);
		powered_tramtype_list:       ["RAIL", "ELRL"];
		tramtype_flags:              bitmask(TRAMTYPE_FLAG_CATENARY);
		construction_cost:           16;
		maintenance_cost:            24;
		sort_order:                  02;	
	}
	graphics {
		gui:						 ELRL_guiSpriteset;
		track_overlay:               ELRL_overlaySpriteset;
		underlay:                    ELRL_underlayTerrainCheckSwitch;
		bridge_surfaces:             ELRL_bridgesTerrainCheckSwitch;
		catenary_front:              ELRL_catenaryFrontTownZoneSwitch;
		catenary_back:               ELRL_catenaryBackTownZoneSwitch;
		depots:	                     ELRL_depotsTerrainCheckSwitch;
	}
}
